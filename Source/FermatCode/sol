; (c) Roman Lee 2018
; Library of functions for LiteRed2 program

  &(u=4);

; Globals

; database
;  db[1,1] Sparse;

; eqs
; eqs[1,1] Sparse;

; wanted
; wanted[1,1] Sparse;

; remapping index
; remap[1,1] Sparse;

; found rule
Array rule[1,1] Sparse;


; NZcols([m],i) returns the the number of nonzero elements on the ith row.
Function NZcols(m,i,j,k) = for j = [m]i do k:+; od; k.;


; Firstcol([m],i) returns the index of the first nonzero element on the ith row.
Function Firstcol(m,i,j) = for j = [m]i do &> od; j.;

; Secondcol([m],i) returns the index of the second nonzero element on the ith row.
Function Secondcol(m,i,j,k) =
  for j = [m]i do
    if k>0 then
      k:=j;
      &>
    fi;
    k:=Cols[m]+1;
  od;
  k.;

; Lastcol([m],i) returns the index of the last nonzero element on the ith row.
  Function Lastcol(m,i,j) = for j = [m]i do &] od; j.;

; Lastrow([m]) returns the index of the last row containing nonzero elements.
Function Lastrow(m,i,last,j) =
  if i=0 then i := Rows[m] fi;
  while i>0 do
    for j = [m]i do
      last:=i;
      i:=1;
      &>
    od;
    i:-
  od;
  last.;

; Rpos(eqs,list) returns the index of the first row with
; first nonzero column index being in list.
Function Rpos(eqs,list,r,i,j,k) =
  if r=0 then
    r := Rows[eqs]
  fi;
  for i = 1,r do
    for j = [eqs]i do
      if list[1,j]=1 then k := i fi;
      & >;
    od;
    if k>0 then &> fi;
  od;
  k.;

; Ulist(eqs,list) updates list to contain the integrals lying above the ones being in list.
; Ulist(eqs,list,r) restricts the search within r first rows
Function Ulist(eqs,list,r,i,j,k) =
  if r=0 then r := Rows[eqs] fi;
  Array dlist[1,Cols[list]];
  for i = 1,r do
    for j = [eqs]i do
      if list[1,j]=1 then
        for k = [dlist]1 do
          list[1,k]:=1;
        od;
        & >
      fi;
      dlist[1,j] := 1;
    od;
  od;
  @([dlist]);
  0.;

; SortUp(eqs,list) sorts the rows putting rows containing list first
; SortUp returns the index of the last row containing indices from list list.
Function SortUp(eqs,list,r,i,j,k) =
  r := Rows[eqs];
  for i = 1,r do
    for j = [eqs]i do
      if list[1,j]=1 then
        k:+;
        Switchrow([eqs],k,i);
        &>
      fi
    od
  od; {here sorting of the first k lines should happen}
  k.;

Function Member(list,el,re,i) =
  for i = [list]1 do
    if i = el then
      re:=1;
      &>
    fi;
  od;
  re.;

Function SparseCopy(new,mat,off,skip,lst,i,j) =
  if lst=0 then lst:=Rows[mat] fi;
  lst:-(skip);
  for i = 1,lst do
    for j = [mat](skip+i) do
      new[off+i,j] := mat[skip+i,j]
    od
  od;
  lst.;

Function RareCopy(new,mat,inds,off,skip,lst,i,j) =
  {RareCopy([new],[mat],[inds],,,)}
  if lst=0 then lst:=Rows[mat] fi;
  lst:-(skip);
  for i = 1,lst do
    for j = [mat](skip+i) do
      new[off+i,inds[j]] := mat[skip+i,j]
    od
  od;
  lst.;


Function IncRowech(mat,eqs,ind,l,h,h1,i,o,p,j,w,c) =
  {
    IncRowech --- incremental row echelon form.`
      mat --- rules database`
      eqs --- equations database`
  }
  w := Cols[eqs];
  l:+;
  for j = [eqs]ind do mat[l,j] := eqs[ind,j] od;
  {leftmost column}
  h := Firstcol([mat],l);
  if h = 0 then l:- else {equation is 0=0}
    for p = 1,l do
      if p = l then h1 := 0 else
        i := perm[p];
        h1 := Firstcol([mat],i)
      fi;
      if h1 = 0 or h < h1 then
        {recalculate perm}
        for j = l-1,p,-1 do perm[j+1]:=perm[j] od;
        perm[p]:=l;
        &>
      else if h = h1 then
        {switching rows}
;        if Lastcol([mat],l)<Lastcol([mat],i) then Switchrow([mat],i,l) fi;
        {modify new row}
        c := - mat[l,h]/mat[i,h];
        for j = [mat]i,l do mat[l,j] := mat[l,j]+mat[i,j]*c od;
        h := Firstcol([mat],l);
        if h = 0 then l:-;&> fi;
      fi fi
    od;
    if h>0 then
      c:=-1/mat[l,h];
      for j = [mat]l do mat[l,j] := mat[l,j]*c od
    fi
  fi;
  l.;


; Hound
Function Fetch(from, lst, off,w) =
  { `
    1. db=[ ], remap=[ ], eqs=[*]: clean start`
    2. db=[*], remap=[ ], eqs=[*]: continue same level`
    3. db=[*], remap=[*], eqs=[*]: continue new level`
  }
  w := Cols[eqs];
  lst := Lastrow([eqs]);
  off := Lastrow([db]);
  if off=0 then
    {clean start}
    @[db]; Array db[lst,w] Sparse;
    Array perm[lst];
    [perm]:=lst
  else
    {continuing}
    {reinit db}
    Array dbnew[off+lst,w] Sparse;
    if Lastrow([remap])=0 then
      {no remapping needed}
      SparseCopy([dbnew],[db],0,0,off)
    else
      {remapping needed}
      RareCopy([dbnew],[db],[remap],0,0,off)
    fi;
    {permutations}
    Array perm1[off+lst];
    [perm1]:=off+lst;
    [perm1[~off]]:=[perm[~off]];
    @[perm];Rname[perm1]:='[perm]';
    @[db];Rname[dbnew]:='[db]'
  fi;
  @[remap];
  rpos := Rpos([db],[wanted],off);
  while from<lst do
    {check whether there is already a rule}
    if rpos>0 then &> fi;
    {not found yet, add rules}
    from:+;
    off:=IncRowech([db],[eqs],from,off);
    rpos := Rpos([db],[wanted],off);
    !!('1 eqs solved.');
  od;
  if rpos>0 then
    {save rule}
    @[rule]; Array rule[1,w] Sparse;  {respawn}
    SparseCopy([rule],[db],0,rpos-1,rpos);
    {save remaing eqs}
    if lst>from then
      {some equations left}
      Array remap[1,w] Sparse;
      Array eqsr[lst-from,w] Sparse;
      SparseCopy([eqsr],[eqs],0,from,lst);
      @[eqs]; Rname[eqsr] := '[eqs]'
    else
      {all equations are solved}
      @[eqs]
    fi
  else
    @[rule]; Array rule[1,w] Sparse;
    @[eqs]
  fi;
  @[wanted];{always reappend wanted}
  rpos.;

rpos := 0;

[dyn] := [(1)];

Array db[1,1] Sparse;
